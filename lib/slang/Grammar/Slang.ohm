
Slang {
  Program = listOf<Stmt, br> br?

  Stmt 
    = Comment 
    | Assignment
    | LambdaFuncall
    | Expr
    
  Defun
    = (ParamList | ListOf<Param, ",">) "=>" (Block | Expr)

  Assignment = (ListIndex | DotAccess | ident) "=" Expr

  Block = "{" Program "}"

  Expr = Defun
    | Ternary
    | Comparison
    | AddExp
    
  Comparison
    = AddExp ">"  AddExp -- gt
    | AddExp "<"  AddExp -- lt
    | AddExp "==" AddExp -- eq
    | AddExp "<=" AddExp -- lte
    | AddExp ">=" AddExp -- gte
    

  Ternary = (~#"?" Condition) "?" Expr ":" Expr

  Condition = Comparison | Expr

  AddExp
    = AddExp "+" MulExp  -- plus
    | AddExp "-" MulExp  -- minus
    | MulExp

  MulExp
    = MulExp "*" ExpExp  -- times
    | MulExp "/" ExpExp  -- divide
    | ExpExp

  ExpExp
    = PriExp "^" ExpExp  -- power
    | PriExp

  PriExp
    = "(" Expr ")"  -- paren
    | "+" PriExp   -- pos
    | "-" PriExp   -- neg
    | PriVal
  
  PriVal = StrLit
         | ArrayLiteral
         | HashLit
         | ListIndex
         | Funcall
         | DotAccess
         | LenLit
         | ident
         | number

  LenLit
    = "#" Expr

  DotAccess 
    = (DotAccess | ident) "." ident

  HashLit 
    = "{" ListOf<(HashKeyVal | Dots), ","> "}"

  HashKeyVal = ident ":" Expr

  Callable = Funcall | DotAccess | ident

  Arg = LambdaFuncall | Expr

  Funcall = Callable "(" listOf<Arg, ","> ")"
  LambdaFuncall = (DotAccess | ident) #" " (~#br nonemptyListOf<Arg, ",">)

  Param = ident
  ParamList
    = "(" ListOf<Param, ","> ")"

  ArrayLiteral
    = "[" ListOf<(Expr | Dots), ","> "]"

  Dots
    = "..." (Funcall | ListIndex | ident)

  ListIndex
    = (ident) "[" Expr "]"

  StrLit
    = "'" #(~"'" any)+ "'"

  Comment
    = "//" #(~newline any)+ newline*
  newline = "\n"

  ident  (an identifier)
   = letter alnum*

  number  (a number)
    = digit* "." digit+  -- fract
    | digit+             -- whole

  br
    = "\r" | "\n"
    | ";"

}
